def TRAINING_DATA_ZIP = 'trdata' + File.separator + 'trdata.zip'
def NETWORK_FILE = File.createTempDir().toString() + File.separator + 'network.eg'


group 'codenn'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task runTraining(type: JavaExec, dependsOn: build) {
    main = 'org.kelog.core.Trainer'
    classpath = sourceSets.main.runtimeClasspath
    args TRAINING_DATA_ZIP, NETWORK_FILE
}

// Worse, but simpler than using node plugin to get node
// just to use bower once...
task bowerInstall(type: Exec) {
    workingDir 'web-app'
    commandLine  'bower', 'install'
}

task runServer(type: JavaExec, dependsOn: [build, bowerInstall, runTraining]) {
    main = 'org.kelog.web.Main'
    classpath = sourceSets.main.runtimeClasspath
    args TRAINING_DATA_ZIP, NETWORK_FILE
}


dependencies {
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.2'
    compile group: 'org.encog', name: 'encog-core', version: '3.3.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.2.4'
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    compile group: 'net.lingala.zip4j', name: 'zip4j', version: '1.3.2'
}
